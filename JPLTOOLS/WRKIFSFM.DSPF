     A*%%TS  SD  20140807  083458  LAMONTRE    REL-V7R1M0  5770-WDS
     A*=========================================================================
     A* SI LE SFLMSG NE S'AFFICHE PAS, VÉRIFIER
     A*  AUCUN FORMAT N'ÉCRIT SUR LA LIGNE SFLMSGRCD(!INLPGM SNDBRKMSG POLLUE LA
     A*  PMQPGM INITIALISÉ A *
     A*  APPEL : IF ERREUR;CALLP PMQMSG...;W2FLSP='60';ITER;ENDIF;
     A*=========================================================================
     A* TOUCHES DE FONCTIONS ET INDICATEURS *INK
     A* KA=F1, KB=F2, KC=F3, KD=F4, KE=F5, KF=F6, KG=F7, KH=F8, KI=F9, KJ=F10,
     A* KK=F11, KL=F12, KM=F13, KN=F14, KP=F15, KQ=F16, KR=F17, KS=F18, KT=F19,
     A* KV=F21, KW=F22, KX=F23, KY=F24
     A*=========================================================================
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT(UL)
     A                                      PRINT
     A                                      HLPTITLE('work with IFS stream file-
     A                                      s')
     A                                      HLPPNLGRP(INTRO WRKIFSHP)
     A                                      HLPFULL
     A                                      INDARA
     A                                      ALTHELP(CA01)
     A                                      WDWBORDER((*COLOR BLU))
     A                                      CF05(05)
     A                                      CA03(03)
     A                                      CF16(16)
     A                                      CA12(12)
     A                                      HELP
     A*=========================================================================
     A          R FMTA
     A*%%TS  SD  20140807  083138  LAMONTRE    REL-V7R1M0  5770-WDS
     A                                      CF04(04)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(FMTA WRKIFSHP)
     A                                      HLPARA(*NONE)
     A*         H                           HLPPNLGRP(FMTA WRKIFSHP)
     A*                                     HLPARA(*NONE)
     A            SPNAME        10A  O  1  2COLOR(BLU)
     A                                  1 13'A'
     A                                  1 15DATE
     A                                      COLOR(BLU)
     A                                      EDTWRD('  /  /  ')
     A                                  1 24TIME
     A                                      COLOR(BLU)
     A                                  1 33SYSNAME
     A                                      COLOR(BLU)
     A                                  1 42USER
     A                                      COLOR(BLU)
     A            WENV           2A  O  1 53COLOR(BLU)
     A            WLIBEN        10A  O  1 56COLOR(BLU)
     A                                  2 13'Afficher un fichier IFS'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A                                  9  1'Fichier.....'
     A            FAIFS        240A  B 10  1
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  COLOR(WHT)
     A                                      CHECK(LC)
     A*=========================================================================
     A          R FMTKA
     A*%%TS  SD  20140807  083458  LAMONTRE    REL-V7R1M0  5770-WDS
     A                                      TEXT('Command keys')
     A          H                           HLPPNLGRP(FMTKA WRKIFSHP)
     A                                      HLPARA(*NONE)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'F3=Quitter'
     A                                      COLOR(BLU)
     A                                 23 13'F12=Abandonner'
     A                                      COLOR(BLU)
     A                                 23 28'F5=Rafraichir'
     A                                      COLOR(BLU)
     A*=========================================================================
     A          R SFLB                      SFL
     A*%%TS  SD  20140807  083458  LAMONTRE    REL-V7R1M0  5770-WDS
     A  60                                  SFLNXTCHG
     A            SBOFST         9Y 0O  7  2EDTCDE(4)
     A            SBHEX1        10A  O  7 13
     A            SBHEX2        10A  O  7 24
     A            SBHEX3        10A  O  7 35
     A            SBHEX4        10A  O  7 46
     A                                  7 57'|'
     A            SBEBCD        20A  O  7 59
     A*=========================================================================
     A          R CTLB                      SFLCTL(SFLB)
     A*%%TS  SD  20140807  083458  LAMONTRE    REL-V7R1M0  5770-WDS
     A                                      ROLLUP(25)
     A                                      KEEP
     A                                      OVERLAY
     A  52                                  SFLDSP
     A N51                                  SFLDSPCTL
     A  51                                  SFLCLR
     A  53                                  SFLEND
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  98                                  SFLMSG('Limite de défilement attein-
     A                                      te')
     A          H                           HLPPNLGRP(CTLB WRKIFSHP)
     A                                      HLPARA(*NONE)
     A            RAN01B         4S 0H      SFLRCDNBR(CURSOR)
     A            SPNAME        10A  O  1  2COLOR(BLU)
     A                                  1 13'B'
     A                                  1 15DATE
     A                                      COLOR(BLU)
     A                                      EDTWRD('  /  /  ')
     A                                  1 24TIME
     A                                      COLOR(BLU)
     A                                  1 33SYSNAME
     A                                      COLOR(BLU)
     A                                  1 42USER
     A                                      COLOR(BLU)
     A            WENV           2A  O  1 53COLOR(BLU)
     A            WLIBEN        10A  O  1 56COLOR(BLU)
     A            CBIFS         80A  O  2  1DSPATR(HI)
     A                                      COLOR(WHT)
     A                                  4  2'  Déplacement         Hexa        -
     A                                                           | Clair       -
     A                                               '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  1'(File starts at 0 )'
     A                                  5 33'   1 1 1 1 1  1 1 1 1 1 |         -
     A                                        1111111111'
     A            CBFROM         9Y 0B  6  2EDTCDE(3)
     A                                      DSPATR(PC)
     A                                  6 12'+ 0 1 2 3 4  5 6 7 8 9  0 1 2 3 4 -
     A                                       5 6 7 8 9 | 01234567890123456789 '
     A                                      DSPATR(UL)
     A            CBSIZE         9Y 0O  3  2EDTCDE(3)
     A**                                    DSPATR(PC)
     A                                  3 12'Bytes'
     A*=========================================================================
     A          R FMTKB
     A*%%TS  SD  20140807  083458  LAMONTRE    REL-V7R1M0  5770-WDS
     A                                      TEXT('Command keys')
     A          H                           HLPPNLGRP(FMTKB WRKIFSHP)
     A                                      HLPARA(*NONE)
     A*         H                           HLPPNLGRP(FMTKB WRKJOBHP)
     A*                                     HLPARA(*NONE)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'F3=Quitter'
     A                                      COLOR(BLU)
     A                                 23 13'F12=Abandonner'
     A                                      COLOR(BLU)
     A                                 23 28'F5=Rafraichir'
     A                                      COLOR(BLU)
     A*===========================================================²=============
     A*=========================================================================
     A          R SFLC                      SFL
     A*%%TS  SD  20130827  080813  LAMONTRE    REL-V7R1M0  5770-WDS
     A  60                                  SFLNXTCHG
     A            SCSELE         5A  B  6  2
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  COLOR(WHT)
     A            SCNAME        50A  O  6  8
     A            SCTYPE        10A  O  6 59
     A            SCTEXT        70A  O  7  8
     A            SCIFS        500A  H
     A*=========================================================================
     A          R CTLC                      SFLCTL(SFLC)
     A*%%TS  SD  20140807  083414  LAMONTRE    REL-V7R1M0  5770-WDS
     A                                      CF04(04)
     A                                      CF10(10)
     A                                      ROLLUP(25)
     A                                      KEEP
     A                                      OVERLAY
     A  52                                  SFLDSP
     A N51                                  SFLDSPCTL
     A  51                                  SFLCLR
     A  53                                  SFLEND
     A N07                                  SFLDROP(CF07)
     A  07                                  SFLFOLD(CF07)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A  98                                  SFLMSG('Limite de défilement attein-
     A                                      te')
     A          H                           HLPPNLGRP(CTLC WRKIFSHP)
     A                                      HLPARA(*NONE)
     A            RAN01C         4S 0H      SFLRCDNBR(CURSOR)
     A            SPNAME        10A  O  1  2COLOR(BLU)
     A                                  1 13'C'
     A                                  1 15DATE
     A                                      COLOR(BLU)
     A                                      EDTWRD('  /  /  ')
     A                                  1 24TIME
     A                                      COLOR(BLU)
     A                                  1 33SYSNAME
     A                                      COLOR(BLU)
     A                                  1 42USER
     A                                      COLOR(BLU)
     A            WENV           2A  O  1 53COLOR(BLU)
     A            WLIBEN        10A  O  1 56COLOR(BLU)
     A            CCIFS         80A  O  2  1DSPATR(HI)
     A                                      COLOR(WHT)
     A                                  3  2'5=next level or dump'
     A                                  4  2'S      LINK                       -
     A                                                               TYPE ...  -
     A                                               '
     A                                      DSPATR(UL)
     A                                  5  2'~'
     A            CCWILD         2A  B  5  4
     A            CCNAME        50A  B  5  8
     A            CCTYPE        10A  B  5 59
     A*=========================================================================
     A          R FMTKC
     A*%%TS  SD  20140807  083458  LAMONTRE    REL-V7R1M0  5770-WDS
     A                                      TEXT('Command keys')
     A          H                           HLPPNLGRP(FMTKC WRKIFSHP)
     A                                      HLPARA(*NONE)
     A*         H                           HLPPNLGRP(FMTKC WRKJOBHP)
     A*                                     HLPARA(*NONE)
     A            FCCMD         78A  O 22  2DSPATR(UL)
     A                                 23  2'F3=Quitter'
     A                                      COLOR(BLU)
     A                                 23 13'F12=Abandonner'
     A                                      COLOR(BLU)
     A                                 23 28'F5=Rafraichir'
     A                                      COLOR(BLU)
     A                                 23 42'F7=DETAILS'
     A                                      COLOR(BLU)
     A                                 23 54'F10=JOBLOG'
     A                                      COLOR(BLU)
     A*===========================================================²=============
      *=========================================================================
                R PMQSFL                    TEXT('Program messages')
                                            SFL SFLMSGRCD(24)
                  MSGKEY                    SFLMSGKEY
                  PMQPGM                    SFLPGMQ
      *=========================================================================
                R PMQCTL                    TEXT('Program messages')
                                            SFLCTL(PMQSFL)
                                            SFLPAG(01) SFLSIZ(03)
                                            OVERLAY PUTOVR
                                            SFLINZ SFLDSP SFLDSPCTL
        01
      ON01                                  SFLEND
                  PMQPGM                    SFLPGMQ
