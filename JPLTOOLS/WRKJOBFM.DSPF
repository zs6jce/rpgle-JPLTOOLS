     A*%%TS  SD  20070523  142617  EB10168     REL-V5R4M0  5722-WDS
     A*=========================================================================
     A* IF SFLMSG DON'T SHOW, CHECK
     A*  NO FORMAT WRITE ON SFLMSGRCD LINE (!INLPGM SNDBRKMSG DIRTY THE FORMAT W
     A*  PMQPGM INIT TO *
     A*  STANDARD CALL : IF ERREUR;CALLP PMQMSG...;W2FLSP='60';ITER;ENDIF;
     A*=========================================================================
     A* FUNCTION KEYS & INDICATORS *INK
     A* KA=F1, KB=F2, KC=F3, KD=F4, KE=F5, KF=F6, KG=F7, KH=F8, KI=F9, KJ=F10,
     A* KK=F11, KL=F12, KM=F13, KN=F14, KP=F15, KQ=F16, KR=F17, KS=F18, KT=F19,
     A* KV=F21, KW=F22, KX=F23, KY=F24
     A*=========================================================================
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT(UL)
     A                                      HLPTITLE('WRKJOBS')
     A                                      HLPPNLGRP(INTRO WRKJOBHP)
     A                                      HLPFULL
     A                                      HELP
     A                                      ALTHELP(CA01)
     A                                      PRINT
     A                                      INDARA
     A                                      WDWBORDER((*COLOR BLU))
     A                                      CF05
     A                                      CA03
     A                                      CA12
     A*=========================================================================
     A* B SUBFILE: FIELD PREFIX IS SB
     A*=========================================================================
     A          R SFLB                      SFL
     A*%%TS  SD  20061220  171344  EB10168     REL-V5R3M0  5722-WDS
     A  60                                  SFLNXTCHG
     A            ZBSELE         2Y 0B  7  2
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A N61                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            SBJOBNAME     10A  O  7  5
     A  62                                  DSPATR(RI)
     A            SBUSERNAME    10A  O  7 16
     A  62                                  DSPATR(RI)
     A            SBJOBNBR       6A  O  7 27
     A  62                                  DSPATR(RI)
     A            SBCURUSER     10A  O  7 34
     A  62                                  DSPATR(RI)
     A            SBIP          15A  O  7 45
     A  62                                  DSPATR(RI)
     A            SBSBS         10A  O  7 61
     A  62                                  DSPATR(RI)
     A            SBSTS          9A  O  7 72
     A  62                                  DSPATR(RI)
     A*=========================================================================
     A* B SUBFILE CONTROLER: FIELD PREFIX IS CB
     A*=========================================================================
     A          R CTLB                      SFLCTL(SFLB)
     A*%%TS  SD  20070523  142617  EB10168     REL-V5R4M0  5722-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      CF16
     A                                      CA10
     A                                      CA21
     A                                      ROLLUP(11)
     A                                      KEEP
     A                                      OVERLAY
     A  52                                  SFLDSP
     A N51                                  SFLDSPCTL
     A N51                                  INVITE
     A  51                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSG('Limite de défilement attein-
     A                                      te')
     A                                      RTNCSRLOC(&RTNCSRFMT &RTNCSRFLD)
     A          H                           HLPPNLGRP(CTLB WRKJOBHP)
     A                                      HLPARA(*NONE)
     A            RTNCSRFMT     10A  H
     A            RTNCSRFLD     10A  H
     A            RAN01B         4S 0H      SFLRCDNBR(CURSOR)
     A            SPNAME        10A  O  1  2COLOR(BLU)
     A                                  1 13'B'
     A                                  1 15DATE
     A                                      COLOR(BLU)
     A                                      EDTWRD('  /  /  ')
     A                                  1 24TIME
     A                                      COLOR(BLU)
     A                                  1 33SYSNAME
     A                                      COLOR(BLU)
     A                                  1 42USER
     A                                      COLOR(BLU)
     A            WENV           2A  O  1 53COLOR(BLU)
     A            WLIBEN        10A  O  1 56COLOR(BLU)
     A            CBCOUNT        6Y 0O  1 69EDTCDE(3)
     A                                  1 76'Lines'
     A                                  2  2'Filter with'
     A                                  2 28'Work with jobs'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A                                  3  2'S '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            CBJOBNAME     10A  B  4  5
     A            CBUSERNAME    10A  B  4 16
     A            CBJOBNBR       6A  B  4 27
     A            CBCURUSER     10A  B  4 34
     A            CBIP          15A  B  4 45
     A            CBSBS         10A  B  4 61
     A            CBSTS          9A  B  4 72VALUES('    ' 'BSCA' 'BSCW' 'CMNA' -
     A                                      'CMNW' 'CMTW' 'CNDW' 'CPCW' 'DEQA' -
     A                                      'DEQW' 'DKTA' 'DKTW' 'DLYW' 'DSC ' -
     A                                      'DSPA' 'DSPW' 'END ' 'EOFA' 'EOFW' -
     A                                      'EOJ ' 'EVTW' 'GRP' 'HLD ' 'HLDT' '-
     A                                      ICFA' 'ICFW' 'INEL' 'JVAA' 'JVAW' '-
     A                                      LCKW' 'LSPA' 'LSPW' 'MLTA' 'MLTW' '-
     A                                      MSGW' 'MTXW' 'MXDW' 'OPTA' 'OPTW' '-
     A                                      OSIW' 'PRTA' 'PRTW' 'PSRW' 'RUN ' '-
     A                                      SELW' 'SEMW' 'SIGS' 'SIGW' 'SRQ ' '-
     A                                      SVFA' 'SVFW' 'TAPA' 'TAPW' 'THDW' '-
     A                                      TIMA' 'TIMW')
     A                                  5 71' Limit to'
     A                                  6  2'Option : same as WRKJOB 4=spool 10-
     A                                      =joblog ... (F1=help)             '
     A                                      DSPATR(UL)
     A            SQLMAX         4Y 0B  6 71EDTCDE(3)
     A                                  6 76'Lines'
     A                                      DSPATR(UL)
     A            CBSTATUS      10A  B  5  2VALUES('*ACTIVE' '*ALL' '*JOBQ' '*O-
     A                                      UTQ' ' ')
     A                                  3  5'JobName   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A  71                                  COLOR(PNK)
     A                                  3 16'UserName  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A  72                                  COLOR(PNK)
     A                                  3 27'JobNbr'
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A  73                                  COLOR(PNK)
     A                                  3 34'Cur User  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A  74                                  COLOR(PNK)
     A                                  3 45'IP address     '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A  75                                  COLOR(PNK)
     A                                  3 61'sbs       '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A  76                                  COLOR(PNK)
     A                                  3 72'Status  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A  77                                  COLOR(PNK)
     A*=========================================================================
     A* B FORMAT FOOTER
     A*=========================================================================
     A          R FMTKB
     A*%%TS  SD  20070523  090447  EB10168     REL-V5R4M0  5722-WDS
     A                                      TEXT('Command keys')
     A          H                           HLPPNLGRP(FMTKB WRKJOBHP)
     A                                      HLPARA(*NONE)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'F3=Exit   '
     A                                      COLOR(BLU)
     A                                 23 13'F12=Cancel    '
     A                                      COLOR(BLU)
     A                                 23 28'F5=Refresh   '
     A                                      COLOR(BLU)
     A                                 23 42'F16=Sort'
     A                                      COLOR(BLU)
     A                                 23 51'F10=Cmd'
     A                                      COLOR(BLU)
     A                                 23 59'F21=CmdLn'
     A                                      COLOR(BLU)
     A*=========================================================================
     A* D FORMAT : FIELD PREFIX IS FD
     A*=========================================================================
     A          R FMTD

     A                                      WINDOW(&FMTDY &FMTDX 7 60)
     A                                      WDWBORDER((*COLOR BLU) (*DSPATR RI)-
     A                                       (*CHAR '...:::.:'))
     A            FMTDX          3S 0P
     A            FMTDY          3S 0P
     A            FDSELECT      59A  O  1  2
     A            FDFROM        59A  O  2  2
     A            FDWHERE       59A  O  3  2
     A                                  5  2'This filter returns.....'
     A            FDCOUNT        4Y 0O  5 27EDTCDE(4)
     A                                  5 32'lines. '
     A            FDYES          3A  B  6 27VALUES('YES' 'NO')
     A                                  6  2'Continue................'
     A                                  6 32'(YES/NO)'
     A          R FMTE

     A                                      WINDOW(&FMTEY &FMTEX 7 60)
     A                                      WDWBORDER((*COLOR BLU) (*DSPATR RI)-
     A                                       (*CHAR '...:::.:'))
     A            FMTEX          3S 0P
     A            FMTEY          3S 0P



     A                                  5  2'CONFIRM EXIT............'


     A            FEYES          3A  B  6 27VALUES('YES' 'NO')
     A                                  6  2'Continue................'
     A                                  6 32'(YES/NO)'
      *=========================================================================
      * MESSAGE SUBFILE
      *=========================================================================
                R PMQSFL                    TEXT('Program messages')
                                            SFL SFLMSGRCD(24)
                  MSGKEY                    SFLMSGKEY
                  PMQPGM                    SFLPGMQ
      *=========================================================================
                R PMQCTL                    TEXT('Program messages')
                                            SFLCTL(PMQSFL)
                                            SFLPAG(01) SFLSIZ(03)
                                            OVERLAY PUTOVR
                                            SFLINZ SFLDSP SFLDSPCTL
        01
      ON01                                  SFLEND
                  PMQPGM                    SFLPGMQ
