     A*%%TS  SD  20110929  075332  LAMONTRE    REL-V7R1M0  5770-WDS
     A*
     A* CRTDSPF FILE(JPL/RTVSQLFM) SRCFILE(JPL/QPGMSRC) SRCMBR(RTVSQLFM) RSTDSP(
     A* REPLACE(*YES)
     A*
     A*
     A*=========================================================================
     A* SI LE SFLMSG NE S'AFFICHE PAS, VÉRIFIER
     A*  AUCUN FORMAT N'ÉCRIT SUR LA LIGNE SFLMSGRCD(!INLPGM SNDBRKMSG POLLUE LA
     A*  PMQPGM INITIALISÉ A *
     A*  APPEL : IF ERREUR;CALLP PMQMSG...;W2FLSP='60';ITER;ENDIF;
     A*=========================================================================
     A* TOUCHES DE FONCTIONS ET INDICATEURS *INK
     A* KA=F1, KB=F2, KC=F3, KD=F4, KE=F5, KF=F6, KG=F7, KH=F8, KI=F9, KJ=F10,
     A* KK=F11, KL=F12, KM=F13, KN=F14, KP=F15, KQ=F16, KR=F17, KS=F18, KT=F19,
     A* KV=F21, KW=F22, KX=F23, KY=F24
     A*=========================================================================
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT(UL)
     A                                      HLPTITLE('RTVSQL')
     A                                      HLPPNLGRP(INTRO RTVSQLHP)
     A                                      HLPFULL
     A                                      HELP
     A                                      ALTHELP(CA01)
     A                                      PRINT
     A                                      INDARA
     A                                      WDWBORDER((*COLOR BLU))
     A                                      CF05
     A                                      CA03
     A                                      CA12
     A*=========================================================================
     A          R FMTA
     A*%%TS  SD  20110929  075332  LAMONTRE    REL-V7R1M0  5770-WDS
     A                                      OVERLAY
     A          H                           HLPPNLGRP(FMTA RTVSQLHP)
     A                                      HLPARA(*NONE)
     A            SPNAME        10A  O  1  2COLOR(BLU)
     A                                  1 13'Retrieve SQL source statement'
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      COLOR(BLU)
     A                                      EDTWRD('  /  /  ')
     A                                  1 71SYSNAME
     A                                      COLOR(BLU)
     A                                  2 62TIME
     A                                      COLOR(BLU)
     A                                  2 71USER
     A                                      COLOR(BLU)
     A            WENV           2A  O  3 62COLOR(BLU)
     A            WLIBEN        10A  O  3 65COLOR(BLU)
     A                                  6  2'Object_Name'
     A            FAOBJNAME     60A  B  6 17CHECK(LC)
     A                                  7  2'Object_Library'
     A            FAOBJLIB      60A  B  7 17CHECK(LC)
     A                                  8  2'Object_Type'
     A            FAOBJTYPE     10A  B  8 14
     A                                  8 25'ALIAS'
     A                                  8 31'CONSTRAINT'
     A                                  8 42'FUNCTION'
     A                                  8 51'INDEX'
     A                                  8 57'PROCEDURE'
     A                                  8 67'SCHEMA'
     A                                  9 45'TABLE'
     A                                  9 51'TRIGGER'
     A                                  9 59'TYPE'
     A                                  9 73'VIEW'
     A                                 10  3'Source_File_Name'
     A            FASRCFILE     10A  B 10 23
     A                                 11  3'Source_File_Library'
     A            FASRCLIB      10A  B 11 23
     A                                 12  3'Source_File_Member'
     A            FASRCMBR      10A  B 12 23
     A                                 12 34'create member'
     A            FACRTMBR       1A  B 12 48
     A                                 12 50'0-1'
     A                                 12 54'Replace member'
     A            FAREPLACE      1A  B 12 69
     A                                 12 71'0-1'
     A                                 14  3'Statement_Formatting'
     A            FAFMTOPT       1A  B 14 24
     A                                 14 26'0-1'
     A                                 14 32'Severity_Level'
     A            FASEVLEVEL     4S 0B 14 47
     A                                 14 52'00-39'
     A                                 15  3'Date_Separator'
     A            FADATSEP       1A  B 15 24
     A                                 15 26'/.,-'
     A                                 15 32'Date_Format'
     A            FADATFMT       3A  B 15 46
     A                                 15 50'ISO'
     A                                 15 54'EUR'
     A                                 15 58'JIS'
     A                                 15 62'USA'
     A                                 15 66'MDY'
     A                                 15 70'DMY'
     A                                 15 74'YMS'
     A                                 15 78'JUL'
     A                                 16  3'Time_Separator'
     A            FATIMSEP       1A  B 16 24
     A                                 16 26':.,'
     A                                 16 32'Time_Format'
     A            FATIMFMT       3A  B 16 46
     A                                 16 50'ISO'
     A                                 16 54'EUR'
     A                                 16 58'JIS'
     A                                 16 62'USA'
     A                                 16 66'HMS'
     A                                 17 32'Naming_Option'
     A            FANAMING       3A  B 17 46
     A                                 17 50'SQL-SYS'
     A                                 17 62'Decimal_Point'
     A            FADECPOINT     1A  B 17 76
     A                                 17 78'.,'
     A                                 18  3'Standards_Option'
     A            FASTANDARD     1A  B 18 24
     A                                 18 26'012'
     A                                 18 32'Drop_Option'
     A            FADROP         1A  B 18 46
     A                                 18 50'01'
     A                                 18 62'Message_Level'
     A            FAMSGLVL       4S 0B 18 76
     A                                 19  3'Comment_Option'
     A            FACOMMENT      1A  B 19 24
     A                                 19 32'Label_Option'
     A            FALABEL        1A  B 19 46
     A                                 19 50'01'
     A                                 19 62'Header_Option'
     A            FAHEADER       1A  B 19 76
     A                                 19 78'01'
     A                                 20  3'Trigger_Option'
     A            FATRIGER       1A  B 20 24
     A                                 20 26'01'
     A                                 20 32'Constraint_Option'
     A            FACONSTR       1A  B 20 50
     A                                 20 52'01'
     A                                 20 62'SysName_Option'
     A            FASYSNAM       1A  B 20 77
     A                                 20 79'01'
     A                                 21  3'Privileges_option'
     A            FAPRIVIL       1A  B 21 24
     A                                 21 26'01'
     A                                 21 32'CCSID_option'
     A            FACCSID        1A  B 21 46
     A                                 21 48'01'
     A                                 21 56'CreateReplace_option'
     A            FACRTRPL       1A  B 21 77
     A                                 21 79'01'
     A                                  9 36'SEQUENCE'
     A                                  9 64'VARIABLE'
     A                                  9 78'XSR'
     A*=========================================================================
     A          R FMTKA
     A                                      TEXT('Command keys')
     A          H                           HLPPNLGRP(FMTKA RTVSQLHP)
     A                                      HLPARA(*NONE)
     A                                 22  2'                                  -
     A                                                                         -
     A                                               '
     A                                      DSPATR(UL)
     A                                 23  2'F3=Quitter'
     A                                      COLOR(BLU)
     A                                 23 13'F12=Abandonner'
     A                                      COLOR(BLU)
     A                                 23 28'F5=Rafraichir'
     A                                      COLOR(BLU)
      *=========================================================================
                R PMQSFL                    TEXT('Program messages')
                                            SFL SFLMSGRCD(24)
                  MSGKEY                    SFLMSGKEY
                  PMQPGM                    SFLPGMQ
      *=========================================================================
                R PMQCTL                    TEXT('Program messages')
                                            SFLCTL(PMQSFL)
                                            SFLPAG(01) SFLSIZ(03)
                                            OVERLAY PUTOVR
                                            SFLINZ SFLDSP SFLDSPCTL
        01
      ON01                                  SFLEND
                  PMQPGM                    SFLPGMQ
